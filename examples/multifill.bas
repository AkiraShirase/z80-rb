   1 LET api=VAL "65007": LET free=VAL "65536-`USR`7962": LET total=USR api: DEF FN m(a,s)=USR api: DEF FN t(t)=USR api: DEF FN f()=USR api
   2 LET fill=VAL "36386": LET fillX=VAL "36354": LET fillY=VAL "36355": DEF FN l(x,y)=USR 36352
   5 BORDER VAL "7": PAPER VAL "7": INK VAL "0": BRIGHT VAL "0": OVER VAL "0": INVERSE VAL "0": FLASH VAL "0": CLS : PRINT AT VAL "0",VAL "0";"system free:",free,"multitask free:",total''"Enter`CONTINUE`to go on..."
   6 STOP 
   9 REM #demo 1
  10 LET total=USR api: LET stack=2*INT (total/18)
  15 BRIGHT 1: OVER 0: BORDER 7: PAPER 7: INK 4: CLS 
  20 LET r=175/6: FOR i=0 TO 2: LET x=256*(i+.5)/3: FOR j=0 TO 2: LET y=175*(j+.5)/3: CIRCLE x,y,r: NEXT j: NEXT i
  30 PLOT 0,0: DRAW 255,0: FOR i=0 TO 2: LET x=256*(i+.5)/3: FOR j=0 TO 2: LET y=175*(j+.5)/3: GO SUB 1000: NEXT j: NEXT i
  35 LET stack=2*INT (total/6)
  40 LET d=r*2: FOR i=0 TO 2: LET x=256*(i+.5)/3: FOR j=0 TO 2: LET y=175*(j+.5)/3: PLOT x-r,y-r: DRAW d,0: DRAW 0,d: DRAW -d,0: DRAW 0,-d: NEXT j: LET x=i*256/3: LET y=87: GO SUB 1000: NEXT i
  50 LET x=250: LET y=87: GO SUB 1000
  60 BORDER 4
  70 STOP 
  99 REM #demo 2
 100 LET total=USR api: LET stack=2*INT (total/2)
 110 BORDER 7: PAPER 7: INK 1: BRIGHT 0: OVER 0: CLS 
 120 CIRCLE 128,87.5,88
 130 OVER 1: LET x=128: LET y=87: GO SUB 1000: PLOT 61,38: DRAW 115,115,10000
 140 PLOT 0,0: DRAW 255,0: PLOT 0,87: DRAW 255,0: OVER 0
 145 LET stack=2*INT (total/8)
 150 LET x=20: LET y=40: GO SUB 1000: LET y=127: GO SUB 1000: LET x=235: GO SUB 1000: LET y=40: GO SUB 1000
 160 STOP 
 169 REM #demo 3 - single task
 170 RESTORE : RANDOMIZE USR api: BRIGHT 1: OVER 0: BORDER 7: PAPER 7: INK 0: CLS 
 180 FOR i=1 TO 8: READ x,y: PLOT x,y: READ x,y: DRAW x,y: READ x,y: DRAW x,y: READ x,y: DRAW x,y: READ x,y: RANDOMIZE FN l(x,y): NEXT i
 190 PLOT 41,0: DRAW 0,175: PLOT 215,175: DRAW 0,-175: DRAW -174,0: FOR i=1 TO 4: READ x,y: RANDOMIZE FN l(x,y): NEXT i
 200 DATA 128,87,-30,50,60,0,-30,-50,128,119
 210 DATA 215,175,-57,-38,20,-20,37,58,188,147
 220 DATA 128,87,50,30,0,-60,-50,30,158,87
 230 DATA 215,0,-57,38,20,20,37,-58,188,27
 240 DATA 128,87,-30,-50,60,0,-30,50,128,55
 250 DATA 41,0,57,38,-20,20,-37,-58,68,27
 260 DATA 128,87,-50,30,0,-60,50,30,98,87
 270 DATA 41,175,57,-38,-20,-20,-37,58,68,147
 280 DATA 128,165,205,87,128,10,51,87
 290 STOP 
 299 REM #demo 3 - multitask
 300 RESTORE : LET total=USR api: OVER 0: BRIGHT 1: PAPER 7: BORDER 7: INK 2: CLS : LET stack=2*INT (total/8)
 310 FOR i=1 TO 8: READ x,y: PLOT x,y: READ x,y: DRAW x,y: READ x,y: DRAW x,y: READ x,y: DRAW x,y: READ x,y: GO SUB 1000: NEXT i
 320 PLOT 41,0: DRAW 0,175: PLOT 215,175: DRAW 0,-175: DRAW -174,0: FOR i=1 TO 4: READ x,y: GO SUB 1000: NEXT i
 390 STOP 
 399 REM #demo 4
 400 LET total=USR api: LET stack=2*INT (total/6): BRIGHT 1: OVER 0: INK 2: PAPER 6: BORDER 6: CLS 
 410 LET x=128: LET y=87: CIRCLE 128,87,35: GO SUB 1000: CIRCLE 128,87,60: LET x=168: GO SUB 1000: CIRCLE 128,87,87: LET x=58: GO SUB 1000
 420 PRINT FN f()
 999 STOP : GO TO 10
1000 POKE fillX,x: POKE fillY,175-y
1001 IF FN f()<stack THEN GO TO 1001
1010 LET tid=FN m(fill,stack): RETURN 
9998 STOP 
9999 CLEAR VAL "36351": LOAD ""CODE : LOAD ""CODE : RUN 